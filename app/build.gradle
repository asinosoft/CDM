apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    compileSdkVersion 30
    buildToolsVersion "31.0.0-rc3"
    buildFeatures.viewBinding = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // Автоматический инкремент номера сборки
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def name = versionProps['VERSION_NAME']
        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)


        defaultConfig {
            applicationId "com.asinosoft.cdm"
            minSdkVersion 23
            targetSdkVersion 30
            versionCode code
            versionName "${name}.${code}"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            multiDexEnabled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Manager@${variant.name}_${variant.versionName}.apk"
                }
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.robolectric:robolectric:4.4'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation "org.jetbrains.anko:anko:0.10.8"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.zerobranch:SwipeLayout:1.3.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'

//dependencies
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'

    //Optional: see how to use the utility.
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'
    implementation 'com.github.chenupt.android:dragtoplayout:1.2.1@aar'
    implementation 'com.xw.repo:bubbleseekbar:3.20'
    implementation 'com.github.cdflynn:crossview:v1.0.1'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'

    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

    debugImplementation 'com.willowtreeapps.hyperion:hyperion-core:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-attr:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-build-config:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-crash:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-disk:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-geiger-counter:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-measurement:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-phoenix:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-recorder:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-shared-preferences:0.9.27'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-timber:0.9.27'

    implementation 'com.googlecode.libphonenumber:libphonenumber:5.5'

    //dialer
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.3"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'com.github.gratusik:AGIKSwipeButton:1.0.1'

    // due error: Cannot fit requested classes in a single dex file
    implementation 'com.android.support:multidex:1.0.3'
}
